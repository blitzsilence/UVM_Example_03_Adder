`ifndef __ENV_SV
`define __ENV_SV

class my_env extends uvm_env;
	`uvm_component_utils(my_env)
	
	agent_in      i_agt;
	agent_out     o_agt;
	my_model      mdl;
	my_scoreboard scb;
	
	uvm_tlm_analysis_fifo #(transaction_in)  i_agt_mdl_fifo;
	uvm_tlm_analysis_fifo #(transaction_out) mdl_scb_fifo;
	uvm_tlm_analysis_fifo #(transaction_out) o_agt_scb_fifo;
	
	function new(string name = "my_env", uvm_component parent);
		 super.new(name, parent);
	endfunction

	extern virtual function void build_phase (uvm_phase phase);
	extern virtual function void connect_phase (uvm_phase phase);
 endclass
 
function void my_env::build_phase(uvm_phase phase);
	super.build_phase(phase);
	i_agt = agent_in::type_id::create("i_agt", this);
	o_agt = agent_out::type_id::create("o_agt", this);
	i_agt.is_active = UVM_ACTIVE;
	o_agt.is_active = UVM_PASSIVE;

	mdl = my_model::type_id::create("mdl", this);
	scb = my_scoreboard::type_id::create("scb", this);

	i_agt_mdl_fifo = new("i_agt_mdl_fifo", this);
	mdl_scb_fifo   = new("mdl_scb_fifo", this);
	o_agt_scb_fifo = new("o_agt_scb_fifo", this);
endfunction
		
function void my_env::connect_phase (uvm_phase phase);
	super.connect_phase(phase);
	i_agt.ap.connect(i_agt_mdl_fifo.analysis_export);
	mdl.port.connect(i_agt_mdl_fifo.blocking_get_export);

	mdl.ap.connect(mdl_scb_fifo.analysis_export);
	scb.exp_port.connect(mdl_scb_fifo.blocking_get_export);

	o_agt.ap.connect(o_agt_scb_fifo.analysis_export);
	scb.act_port.connect(o_agt_scb_fifo.blocking_get_export); 
endfunction

`endif
